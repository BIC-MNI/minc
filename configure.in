dnl Process this file with autoconf to produce a configure script.

AC_INIT
AC_CONFIG_SRCDIR([libsrc/minc.h])
AC_CONFIG_AUX_DIR(ac_config_aux)
AM_INIT_AUTOMAKE(minc, 2.0.08)
AC_CONFIG_HEADERS([config.h])

AC_REVISION($Revision: 6.19 $)

AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET

AM_PROG_CC_C_O
AC_PROG_RANLIB
AC_PROG_F77
AM_PROG_LEX
AC_PROG_YACC

AC_HEADER_TIME
AC_CHECK_HEADERS(sys/time.h sys/stat.h sys/wait.h unistd.h)
AC_CHECK_HEADERS(fcntl.h pwd.h float.h values.h)

dnl Build only static libs by default
AC_DISABLE_SHARED
AC_PROG_LIBTOOL

smr_WITH_BUILD_PATH

# Check for required C libraries
mni_REQUIRE_LIB(m,[#include <math.h>],[double x = sqrt(3.);])
mni_REQUIRE_LIB(netcdf,[#include <netcdf.h>],[int i = ncopen("",0);])
mni_REQUIRE_LIB(hdf5,[#include <hdf5.h>],[int f = H5Fopen("",0,H5P_DEFAULT);])

dnl Replacement implementations for systems that lack the function.

dnl need ac 2.57 for this? AC_CONFIG_LIBOBJ_DIR(libsrc)
AC_REPLACE_FUNCS(strdup)

dnl Verify existence of mkstemp, tempnam, and tmpnam
AC_CHECK_FUNCS(mkstemp tempnam tmpnam)

dnl Verify existence of some functions we'd like to use
AC_CHECK_FUNCS(getpwnam select strerror)

# Functions required for execute_decompress_command().
AC_FUNC_FORK
AC_CHECK_FUNCS(system popen)

dnl This link is needed to allow <volume_io/foo.h> style includes.
AC_CONFIG_LINKS(volume_io/Include/volume_io:volume_io/Include)

AC_CONFIG_FILES([
Makefile 
doc/Makefile
testdir/Makefile 
libsrc2/Makefile
libsrc2/test/Makefile
volume_io/Makefile
volume_io/Documentation/Makefile
volume_io/Testing/Makefile
])
AC_OUTPUT

